plugins {
    id 'java'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id "org.asciidoctor.convert" version "1.5.10"
    id 'org.springframework.boot' version "2.2.7.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

group = 'com.kbds.gateway'
version = '0.0.1'
description = 'GatewayPortal'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

asciidoctor {
    dependsOn test
}

task copyDocument(type: Copy){
    dependsOn asciidoctor
    from file("build/asciidoc/html5/")
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

build{
    dependsOn copyDocument
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.2.7.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'com.netflix.feign:feign-jackson:8.18.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.2.7.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.2.7.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-config:2.2.2.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp:2.2.1.RELEASE'
    implementation 'com.auth0:java-jwt:3.8.3'
    implementation 'org.modelmapper:modelmapper:2.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:2.2.7.RELEASE'
    implementation 'com.querydsl:querydsl-jpa:4.2.2'
    implementation 'com.querydsl:querydsl-sql:4.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.4'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.4'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.2.7.RELEASE'
    implementation 'org.apache.maven.plugins:maven-antrun-plugin:1.8'
    implementation 'org.springframework.cloud:spring-cloud-starter-oauth2:2.2.1.RELEASE'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth:2.2.6.RELEASE'
    runtimeOnly 'mysql:mysql-connector-java:8.0.20'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.7.RELEASE'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.4.RELEASE'
    compileOnly 'com.querydsl:querydsl-apt:4.2.2'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

test {
    useJUnitPlatform()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR4'
    }
}

configurations {
    querydsl.extendsFrom compileClasspath
}

//queryDSL 설정
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {
    main.java.srcDir querydslDir
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
